apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.triplet.play'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'realm-android'

final stampVersionCode = hasProperty('version_code') ? property('version_code').toInteger() : 1
final stampVersionName = hasProperty('version_name') ? '0.1.0.' + property('version_name') : '0.1.0.0'

final stampKeystorePassword = hasProperty('keystore_password') ? property('keystore_password') : ''
final stampKeyAlias = hasProperty('key_alias') ? property('key_alias') : ''
final stampKeyPassword = hasProperty('key_password') ? property('key_password') : ''
final stampKeyFile = hasProperty('key_file') ? property('key_file') : 'ci/debug.jks'
final stampPlayAPIJson = hasProperty('play_api_json') ? property('play_api_json') : 'ci/play.json'

android {
    compileSdkVersion 26
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.sjn.stamp"
        minSdkVersion 17
        targetSdkVersion 26
        versionCode stampVersionCode
        versionName stampVersionName
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError true
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }
    signingConfigs {
        src {
            storeFile file(stampKeyFile)
            storePassword stampKeystorePassword
            keyAlias stampKeyAlias
            keyPassword stampKeyPassword
        }
    }
    buildTypes {
        debug {
            debuggable true
        }
        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.src
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all {
        variant -> variant.resValue "string", "versionName", variant.versionName
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven { url "https://jitpack.io" }
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:26.0.0'
    compile 'com.android.support:cardview-v7:26.0.0'
    compile 'com.android.support:leanback-v17:26.0.0'
    compile 'com.android.support:design:26.0.0'
    compile 'com.android.support:mediarouter-v7:26.0.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.google.android.support:wearable:2.0.4'
    compile 'com.google.android.gms:play-services-cast-framework:11.0.4'
    compile 'com.google.firebase:firebase-analytics:10.2.1'
    compile 'com.google.firebase:firebase-core:10.2.1'
    compile 'com.google.firebase:firebase-crash:10.2.1'
    compile 'com.google.guava:guava:20.0'
    compile 'com.bowyer.app:fabtransitionlayout:0.4.0@aar'
    compile('com.github.afollestad.material-dialogs:core:0.8.5.8@aar') {
        transitive = true
    }
    compile('com.github.ozodrukh:CircularReveal:2.0.1@aar') {
        transitive = true;
    }
    compile 'com.github.sjnyag:AnimationWrapLayout:0.1.0'
    compile 'com.github.takusemba:spotlight:1.1.1'
    compile 'com.github.vajro:MaterialDesignLibrary:1.6'
    compile 'com.jakewharton:disklrucache:2.0.2'
    compile('com.mikepenz:materialdrawer:5.3.0@aar') {
        transitive = true
    }
    compile 'com.nanohttpd:nanohttpd-webserver:2.1.1'
    compile 'com.sothree.slidinguppanel:library:3.3.0'
    compile 'com.squareup.picasso:picasso:2.3.3'
    compile 'com.takisoft.fix:preference-v7:26.0.0.0'
    compile 'eu.davidea:flexible-adapter:5.0.0-rc1'
    compile 'net.danlew:android.joda:2.6.0'
    compile 'net.yslibrary.licenseadapter:licenseadapter:1.4.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    kapt 'io.realm:realm-annotations:3.5.0'
    kapt 'io.realm:realm-annotations-processor:3.5.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support:support-annotations:26.0.0'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

play {
    track = 'alpha'
    jsonFile = file(stampPlayAPIJson)
}
apply plugin: 'com.google.gms.google-services'